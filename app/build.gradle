plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    compileSdk 33

    defaultConfig {

        applicationId "alex.zhurkov.gifloader"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

    }

    buildTypes {
        def configGiphyApiKey = "\"ENTER_YOUR_GIPHY_KEY_HERE\""
        debug {
            buildConfigField "String", "GIPHY_API_KEY", configGiphyApiKey
        }
        release {
            buildConfigField "String", "GIPHY_API_KEY", configGiphyApiKey
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def material3Version = "1.0.1"
    def activityComposeVersion = "1.6.1"
    def coreCtxVersion = "1.9.0"
    def daggerVersion = "2.44.2"
    def timberVersion = "4.7.1"
    def retrofitVersion = "2.9.0"
    def interceptorVersion = "4.9.1"
    def coilVersion = "2.1.0"
    def lifeCycleVersion = "2.5.1"
    def lottieVersion = "5.2.0"
    def shimmerVersion = "1.0.3"

    //Test
    def junitVersion = "4.13.2"
    def kotlinReflectVersion = "1.7.20"
    def coreTestingVersion = "2.1.0"
    def mockitoInlineVersion = "3.8.0"
    def mockitoKotlinVersion = "4.1.0"

    implementation "androidx.core:core-ktx:$coreCtxVersion"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:$material3Version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifeCycleVersion"
    implementation "androidx.activity:activity-compose:$activityComposeVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifeCycleVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // Lottie
    implementation "com.airbnb.android:lottie-compose:$lottieVersion"

    // DI
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$interceptorVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Image loading
    implementation "io.coil-kt:coil-compose:$coilVersion"
    implementation "io.coil-kt:coil-gif:$coilVersion"

    // Shimmer
    implementation "com.valentinilk.shimmer:compose-shimmer:$shimmerVersion"

    // Tests
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinReflectVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
}
